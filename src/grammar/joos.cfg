Start BOF CompilationUnit EOF
Start BOF EOF

CompilationUnit PackageDeclaration ImportDeclarations TypeDeclaration
CompilationUnit ImportDeclarations TypeDeclaration
CompilationUnit TypeDeclaration
CompilationUnit PackageDeclaration ImportDeclarations
CompilationUnit PackageDeclaration
CompilationUnit ImportDeclarations
CompilationUnit PackageDeclaration TypeDeclaration

PackageDeclaration PACKAGE Name SEMI

ImportDeclarations ImportDeclarations ImportDeclaration
ImportDeclarations ImportDeclaration
ImportDeclaration SingleTypeImportDeclaration
ImportDeclaration TypeImportOnDemandDeclaration

SingleTypeImportDeclaration IMPORT Name SEMI
TypeImportOnDemandDeclaration IMPORT Name DOT MULTIPLICATION SEMI

TypeDeclaration ClassDeclaration
TypeDeclaration InterfaceDeclaration

ClassDeclaration ModifierList CLASS IDENTIFIER LBRAC ClassBodyDeclarationList RBRAC
ClassDeclaration ModifierList CLASS IDENTIFIER Super LBRAC ClassBodyDeclarationList RBRAC
ClassDeclaration ModifierList CLASS IDENTIFIER Interfaces LBRAC ClassBodyDeclarationList RBRAC
ClassDeclaration ModifierList CLASS IDENTIFIER Super Interfaces LBRAC ClassBodyDeclarationList RBRAC

InterfaceDeclaration ModifierList INTERFACE IDENTIFIER LBRAC RBRAC
InterfaceDeclaration ModifierList INTERFACE IDENTIFIER LBRAC InterfaceBodyDeclarationList RBRAC
InterfaceDeclaration ModifierList INTERFACE IDENTIFIER Extensions LBRAC RBRAC
InterfaceDeclaration ModifierList INTERFACE IDENTIFIER Extensions LBRAC InterfaceBodyDeclarationList RBRAC

Extensions EXTENDS ExtensionList
ExtensionList ExtensionList COMMA ExtensionType
ExtensionList ExtensionType
ExtensionType Name

Super EXTENDS Name
Interfaces IMPLEMENTS InterfaceList
InterfaceList InterfaceList COMMA InterfaceType
InterfaceList InterfaceType
InterfaceType Name

ModifierList ModifierList Modifier
ModifierList Modifier
Modifier PUBLIC
Modifier PROTECTED
Modifier STATIC
Modifier ABSTRACT
Modifier FINAL
Modifier NATIVE

ClassBodyDeclarationList ClassBodyDeclarationList ClassBodyDeclaration
ClassBodyDeclarationList ClassBodyDeclaration
InterfaceBodyDeclarationList InterfaceBodyDeclarationList InterfaceBodyDeclaration
InterfaceBodyDeclarationList InterfaceBodyDeclaration

ClassBodyDeclaration FieldDeclaration
ClassBodyDeclaration ConstructorDeclaration
ClassBodyDeclaration ClassMethodDeclaration
InterfaceBodyDeclaration InterfaceMethodDeclaration
InterfaceMethodDeclaration MethodHeader SEMI

MethodHeader ModifierList Type IDENTIFIER LPAREN RPAREN
MethodHeader ModifierList VoidType IDENTIFIER LPAREN RPAREN
MethodHeader ModifierList Type IDENTIFIER LPAREN FormalParameterList RPAREN
MethodHeader ModifierList VoidType IDENTIFIER LPAREN FormalParameterList RPAREN


ClassMethodDeclaration MethodHeader Block
ClassMethodDeclaration MethodHeader SEMI

ConstructorDeclaration ModifierList IDENTIFIER LPAREN RPAREN Block
ConstructorDeclaration ModifierList IDENTIFIER LPAREN FormalParameterList RPAREN Block
FieldDeclaration ModifierList VariableDeclarator SEMI
FieldDeclaration ModifierList SingleVariableDeclaration SEMI

FormalParameterList FormalParameterList COMMA SingleVariableDeclaration
FormalParameterList SingleVariableDeclaration
SingleVariableDeclaration Type IDENTIFIER

Type PrimitiveType
Type ArrayType
Type SimpleType
PrimitiveType BYTE
PrimitiveType SHORT
PrimitiveType CHAR
PrimitiveType INT
PrimitiveType BOOLEAN
ArrayType PrimitiveType LBRKT RBRKT
ArrayType Name LBRKT RBRKT
SimpleType Name

VoidType VOID

Block LBRAC RBRAC
Block LBRAC StatementList RBRAC

StatementList StatementList Statement
StatementList Statement

# Dangling else problem

Statement IfThenStatement
Statement IfThenElseStatement
Statement WhileStatement
Statement ForStatement
Statement SimpleStatement

IfThenStatement IF LPAREN Expression RPAREN Statement
IfThenElseStatement IF LPAREN Expression RPAREN StatementNoShortIf ELSE Statement
IfThenElseStatementNoShortIf IF LPAREN Expression RPAREN StatementNoShortIf ELSE StatementNoShortIf

StatementNoShortIf SimpleStatement
StatementNoShortIf IfThenElseStatementNoShortIf
StatementNoShortIf WhileStatementNoShortIf
StatementNoShortIf ForStatementNoShortIf

WhileStatement WHILE LPAREN Expression RPAREN Statement
WhileStatementNoShortIf WHILE LPAREN Expression RPAREN StatementNoShortIf

ForStatement FOR LPAREN SEMI SEMI RPAREN Statement
ForStatement FOR LPAREN ForInit SEMI SEMI RPAREN Statement
ForStatement FOR LPAREN ForInit SEMI ForCond SEMI RPAREN Statement
ForStatement FOR LPAREN ForInit SEMI ForCond SEMI ForUpdate RPAREN Statement
ForStatement FOR LPAREN ForInit SEMI SEMI ForUpdate RPAREN Statement
ForStatement FOR LPAREN SEMI ForCond SEMI RPAREN Statement
ForStatement FOR LPAREN SEMI ForCond SEMI ForUpdate RPAREN Statement
ForStatement FOR LPAREN SEMI SEMI ForUpdate RPAREN Statement

ForStatementNoShortIf FOR LPAREN SEMI SEMI RPAREN StatementNoShortIf
ForStatementNoShortIf FOR LPAREN ForInit SEMI SEMI RPAREN StatementNoShortIf
ForStatementNoShortIf FOR LPAREN ForInit SEMI ForCond SEMI RPAREN StatementNoShortIf
ForStatementNoShortIf FOR LPAREN ForInit SEMI ForCond SEMI ForUpdate RPAREN StatementNoShortIf
ForStatementNoShortIf FOR LPAREN ForInit SEMI SEMI ForUpdate RPAREN StatementNoShortIf
ForStatementNoShortIf FOR LPAREN SEMI ForCond SEMI RPAREN StatementNoShortIf
ForStatementNoShortIf FOR LPAREN SEMI ForCond SEMI ForUpdate RPAREN StatementNoShortIf
ForStatementNoShortIf FOR LPAREN SEMI SEMI ForUpdate RPAREN StatementNoShortIf

ForInit ExpressionStatement
ForInit VariableDeclarator
ForCond Expression

ForUpdate ExpressionStatement

SimpleStatement Block
SimpleStatement StatementExpression
SimpleStatement ReturnStatement
SimpleStatement VariableDeclarationStatement
SimpleStatement SEMI

VariableDeclarationStatement VariableDeclarator SEMI
VariableDeclarator SingleVariableDeclaration ASSIGN Expression

StatementExpression ExpressionStatement SEMI

ExpressionStatement ClassInstanceCreation
ExpressionStatement MethodNameInvocation
ExpressionStatement MethodPrimaryInvocation
ExpressionStatement Assignment

ReturnStatement RETURN Expression SEMI
ReturnStatement RETURN SEMI

Name Name DOT IDENTIFIER
Name IDENTIFIER

Expression AssignmentExpression

AssignmentExpression ConditionalOrExpression
AssignmentExpression Assignment

Assignment Name ASSIGN AssignmentExpression
Assignment FieldAccess ASSIGN AssignmentExpression
Assignment ArrayAccess ASSIGN AssignmentExpression

ConditionalOrExpression ConditionalAndExpression
ConditionalOrExpression ConditionalOrExpression OR ConditionalAndExpression

ConditionalAndExpression InclusiveOrExpression
ConditionalAndExpression ConditionalAndExpression AND InclusiveOrExpression

InclusiveOrExpression ExclusiveOrExpression
InclusiveOrExpression InclusiveOrExpression BITWISEOR ExclusiveOrExpression

ExclusiveOrExpression AndExpression
ExclusiveOrExpression ExclusiveOrExpression BITWISEXOR AndExpression

AndExpression EqualityExpression
AndExpression AndExpression BITWISEAND EqualityExpression

EqualityExpression RelationalExpression
EqualityExpression EqualityExpression EQUAL RelationalExpression
EqualityExpression EqualityExpression NOT_EQUAL RelationalExpression

RelationalExpression AdditiveExpression
RelationalExpression InstanceOfExpression
RelationalExpression RelationalExpression LESSER AdditiveExpression
RelationalExpression RelationalExpression GREATER AdditiveExpression
RelationalExpression RelationalExpression LESSER_OR_EQUAL AdditiveExpression
RelationalExpression RelationalExpression GREATER_OR_EQUAL AdditiveExpression
InstanceOfExpression RelationalExpression INSTANCEOF ArrayType
InstanceOfExpression RelationalExpression INSTANCEOF SimpleType

AdditiveExpression MultiplicativeExpression
AdditiveExpression AdditiveExpression ADDITION MultiplicativeExpression
AdditiveExpression AdditiveExpression SUBTRACTION MultiplicativeExpression

MultiplicativeExpression UnaryExpression
MultiplicativeExpression MultiplicativeExpression MULTIPLICATION UnaryExpression
MultiplicativeExpression MultiplicativeExpression DIVISION UnaryExpression
MultiplicativeExpression MultiplicativeExpression MODULUS UnaryExpression

UnaryExpression SUBTRACTION UnaryExpression
UnaryExpression UnaryExpressionNotMinus

UnaryExpressionNotMinus EXCLAMATION UnaryExpression
UnaryExpressionNotMinus CastExpression
UnaryExpressionNotMinus CastType
UnaryExpressionNotMinus PostfixExpression

CastExpression LPAREN Expression RPAREN UnaryExpressionNotMinus
CastType LPAREN ArrayType RPAREN UnaryExpressionNotMinus
CastType LPAREN PrimitiveType RPAREN UnaryExpression

PostfixExpression Name
PostfixExpression Primary

Primary PrimaryExpressionNoNewArray
Primary ArrayCreation

PrimaryExpressionNoNewArray Literal
PrimaryExpressionNoNewArray ThisExpression
PrimaryExpressionNoNewArray LPAREN Expression RPAREN
PrimaryExpressionNoNewArray ClassInstanceCreation
PrimaryExpressionNoNewArray FieldAccess
PrimaryExpressionNoNewArray MethodNameInvocation
PrimaryExpressionNoNewArray MethodPrimaryInvocation
PrimaryExpressionNoNewArray ArrayAccess

MethodNameInvocation Name LPAREN RPAREN
MethodNameInvocation Name LPAREN ArgumentList RPAREN
MethodPrimaryInvocation Primary DOT IDENTIFIER LPAREN RPAREN
MethodPrimaryInvocation Primary DOT IDENTIFIER LPAREN ArgumentList RPAREN

ThisExpression THIS
FieldAccess Primary DOT IDENTIFIER
ArrayAccess Name LBRKT Expression RBRKT
ArrayAccess PrimaryExpressionNoNewArray LBRKT Expression RBRKT

ClassInstanceCreation NEW SimpleType LPAREN RPAREN
ClassInstanceCreation NEW SimpleType LPAREN ArgumentList RPAREN

ArrayCreation NEW PrimitiveType LBRKT Expression RBRKT
ArrayCreation NEW SimpleType LBRKT Expression RBRKT

ArgumentList Arguments
Arguments Arguments COMMA Argument
Arguments Argument
Argument Expression

Literal DEC_INT_LITERAL
Literal BOOLEAN_LITERAL
Literal CHAR_LITERAL
Literal STRING_LITERAL
Literal NULL_LITERAL
