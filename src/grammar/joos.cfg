Start BOF CompilationUnit EOF
Start BOF EOF

CompilationUnit PackageDeclaration ImportDeclarations TypeDeclaration
CompilationUnit ImportDeclarations TypeDeclaration
CompilationUnit TypeDeclaration
CompilationUnit PackageDeclaration ImportDeclarations
CompilationUnit PackageDeclaration
CompilationUnit ImportDeclarations
CompilationUnit PackageDeclaration TypeDeclaration

PackageDeclaration PACKAGE Name SEMI

ImportDeclarations ImportDeclarations ImportDeclaration
ImportDeclarations ImportDeclaration
ImportDeclaration SingleTypeImportDeclaration
ImportDeclaration TypeImportOnDemandDeclaration

SingleTypeImportDeclaration IMPORT Name SEMI
TypeImportOnDemandDeclaration IMPORT Name DOT MULTIPLICATION SEMI

TypeDeclaration ClassDeclaration
TypeDeclaration InterfaceDeclaration

ClassDeclaration ModifierList CLASS IDENTIFIER LBRAC ClassBodyDeclarationList RBRAC
ClassDeclaration ModifierList CLASS IDENTIFIER Super LBRAC ClassBodyDeclarationList RBRAC
ClassDeclaration ModifierList CLASS IDENTIFIER Interfaces LBRAC ClassBodyDeclarationList RBRAC
ClassDeclaration ModifierList CLASS IDENTIFIER Super Interfaces LBRAC ClassBodyDeclarationList RBRAC

InterfaceDeclaration ModifierList INTERFACE IDENTIFIER LBRAC RBRAC
InterfaceDeclaration ModifierList INTERFACE IDENTIFIER LBRAC InterfaceBodyDeclarationList RBRAC
InterfaceDeclaration ModifierList INTERFACE IDENTIFIER Extensions LBRAC RBRAC
InterfaceDeclaration ModifierList INTERFACE IDENTIFIER Extensions LBRAC InterfaceBodyDeclarationList RBRAC

Extensions EXTENDS ExtensionList
ExtensionList ExtensionList COMMA Name
ExtensionList Name

Super EXTENDS Name
Interfaces IMPLEMENTS InterfaceList
InterfaceList InterfaceList COMMA Name
InterfaceList Name

ModifierList ModifierList Modifier
ModifierList Modifier
Modifier PUBLIC
Modifier PROTECTED
Modifier STATIC
Modifier ABSTRACT
Modifier FINAL
Modifier NATIVE

ClassBodyDeclarationList ClassBodyDeclarationList ClassBodyDeclaration
ClassBodyDeclarationList ClassBodyDeclaration
InterfaceBodyDeclarationList InterfaceBodyDeclarationList InterfaceBodyDeclaration
InterfaceBodyDeclarationList InterfaceBodyDeclaration

ClassBodyDeclaration FieldDeclaration
ClassBodyDeclaration ConstructorDeclaration
ClassBodyDeclaration ClassMethodDeclaration
InterfaceBodyDeclaration InterfaceMethodDeclaration
InterfaceMethodDeclaration MethodHeader SEMI

MethodHeader ModifierList Type IDENTIFIER LPAREN RPAREN
MethodHeader ModifierList VOID IDENTIFIER LPAREN RPAREN
MethodHeader ModifierList Type IDENTIFIER LPAREN FormalParameterList RPAREN
MethodHeader ModifierList VOID IDENTIFIER LPAREN FormalParameterList RPAREN


ClassMethodDeclaration MethodHeader Block
ClassMethodDeclaration MethodHeader SEMI

ConstructorDeclaration ModifierList IDENTIFIER LPAREN RPAREN Block
ConstructorDeclaration ModifierList IDENTIFIER LPAREN FormalParameterList RPAREN Block
FieldDeclaration ModifierList Type IDENTIFIER ASSIGN Expression SEMI
FieldDeclaration ModifierList Type IDENTIFIER SEMI

FormalParameterList FormalParameterList COMMA SingleVariableDeclaration
FormalParameterList SingleVariableDeclaration
SingleVariableDeclaration Type IDENTIFIER


Type PrimitiveType
Type ArrayType
Type SimpleType
PrimitiveType BYTE
PrimitiveType SHORT
PrimitiveType CHAR
PrimitiveType INT
PrimitiveType BOOLEAN
ArrayType PrimitiveType LBRKT RBRKT
ArrayType Name LBRKT RBRKT
SimpleType Name

Block LBRAC RBRAC
Block LBRAC StatementList RBRAC

StatementList StatementList Statement
StatementList Statement

# Dangling else problem

Statement IfThenStatement
Statement IfThenElseStatement
Statement WhileStatement
Statement ForStatement
Statement SimpleStatement

IfThenStatement IF LPAREN Expression RPAREN Statement
IfThenElseStatement IF LPAREN Expression RPAREN StatementNoShortIf ELSE Statement
IfThenElseStatementNoShortIf IF LPAREN Expression RPAREN StatementNoShortIf ELSE StatementNoShortIf

StatementNoShortIf SimpleStatement
StatementNoShortIf IfThenElseStatementNoShortIf
StatementNoShortIf WhileStatementNoShortIf
StatementNoShortIf ForStatementNoShortIf

WhileStatement WHILE LPAREN Expression RPAREN Statement
WhileStatementNoShortIf WHILE LPAREN Expression RPAREN StatementNoShortIf

ForStatement FOR LPAREN SEMI SEMI RPAREN Statement
ForStatement FOR LPAREN ForInit SEMI SEMI RPAREN Statement
ForStatement FOR LPAREN ForInit SEMI Expression SEMI RPAREN Statement
ForStatement FOR LPAREN ForInit SEMI Expression SEMI ForUpdate RPAREN Statement
ForStatement FOR LPAREN ForInit SEMI SEMI ForUpdate RPAREN Statement
ForStatement FOR LPAREN SEMI Expression SEMI RPAREN Statement
ForStatement FOR LPAREN SEMI Expression SEMI ForUpdate RPAREN Statement
ForStatement FOR LPAREN SEMI SEMI ForUpdate RPAREN Statement

ForStatementNoShortIf FOR LPAREN SEMI SEMI RPAREN StatementNoShortIf
ForStatementNoShortIf FOR LPAREN ForInit SEMI SEMI RPAREN StatementNoShortIf
ForStatementNoShortIf FOR LPAREN ForInit SEMI Expression SEMI RPAREN StatementNoShortIf
ForStatementNoShortIf FOR LPAREN ForInit SEMI Expression SEMI ForUpdate RPAREN StatementNoShortIf
ForStatementNoShortIf FOR LPAREN ForInit SEMI SEMI ForUpdate RPAREN StatementNoShortIf
ForStatementNoShortIf FOR LPAREN SEMI Expression SEMI RPAREN StatementNoShortIf
ForStatementNoShortIf FOR LPAREN SEMI Expression SEMI ForUpdate RPAREN StatementNoShortIf
ForStatementNoShortIf FOR LPAREN SEMI SEMI ForUpdate RPAREN StatementNoShortIf

ForInit ExpressionStatement
ForInit VariableDeclarator

ForUpdate ExpressionStatement

SimpleStatement Block
SimpleStatement StatementExpression
SimpleStatement ReturnStatement
SimpleStatement VariableDeclarationStatement
SimpleStatement SEMI

VariableDeclarationStatement VariableDeclarator SEMI
VariableDeclarator SingleVariableDeclaration ASSIGN Expression

StatementExpression ExpressionStatement SEMI

ExpressionStatement ClassInstanceCreation
ExpressionStatement MethodInvocation
ExpressionStatement AssignmentExpression

ReturnStatement RETURN Expression SEMI
ReturnStatement RETURN SEMI

Name Name DOT IDENTIFIER
Name IDENTIFIER

Expression AssignmentExpression
Expression OperationExpression

AssignmentExpression Name ASSIGN Expression
AssignmentExpression FieldAccess ASSIGN Expression
AssignmentExpression ArrayAccess ASSIGN Expression

OperationExpression UnaryExpression
OperationExpression OperationExpression BinaryOperator UnaryExpression
OperationExpression OperationExpression InstanceOperator ArrayType
OperationExpression OperationExpression InstanceOperator SimpleType

BinaryOperator ADDITION
BinaryOperator SUBTRACTION
BinaryOperator MULTIPLICATION
BinaryOperator DIVISION
BinaryOperator MODULUS
BinaryOperator EQUAL
BinaryOperator LESSER_OR_EQUAL
BinaryOperator GREATER_OR_EQUAL
BinaryOperator LESSER
BinaryOperator GREATER
BinaryOperator NOT_EQUAL
BinaryOperator OR
BinaryOperator AND
BinaryOperator BITWISEAND
BinaryOperator BITWISEOR
BinaryOperator BITWISEXOR

InstanceOperator INSTANCEOF

UnaryExpression SUBTRACTION UnaryExpression
UnaryExpression UnaryExpressionNotMinus

UnaryExpressionNotMinus EXCLAMATION UnaryExpression
UnaryExpressionNotMinus CastExpression
UnaryExpressionNotMinus PostfixExpression

CastExpression LPAREN Expression RPAREN UnaryExpressionNotMinus
CastExpression LPAREN PrimitiveType RPAREN UnaryExpression
CastExpression LPAREN ArrayType RPAREN UnaryExpression

PostfixExpression Name
PostfixExpression PrimaryExpression

PrimaryExpression PrimaryExpressionNoMultiArray
PrimaryExpression ArrayAccess
PrimaryExpression ArrayCreation

PrimaryExpressionNoMultiArray Literal
PrimaryExpressionNoMultiArray LPAREN Expression RPAREN
PrimaryExpressionNoMultiArray MethodInvocation
PrimaryExpressionNoMultiArray ThisExpression
PrimaryExpressionNoMultiArray FieldAccess
PrimaryExpressionNoMultiArray ClassInstanceCreation

MethodInvocation Name LPAREN RPAREN
MethodInvocation Name LPAREN ArgumentList RPAREN
MethodInvocation PrimaryExpression DOT IDENTIFIER LPAREN RPAREN
MethodInvocation PrimaryExpression DOT IDENTIFIER LPAREN ArgumentList RPAREN

ThisExpression THIS
FieldAccess PrimaryExpression DOT IDENTIFIER
ArrayAccess Name LBRKT Expression RBRKT
ArrayAccess PrimaryExpressionNoMultiArray LBRKT Expression RBRKT

ClassInstanceCreation NEW SimpleType LPAREN RPAREN
ClassInstanceCreation NEW SimpleType LPAREN ArgumentList RPAREN

ArrayCreation NEW PrimitiveType LBRKT Expression RBRKT
ArrayCreation NEW SimpleType LBRKT Expression RBRKT

ArgumentList Arguments
Arguments Arguments COMMA Expression
Arguments Expression

Literal DEC_INT_LITERAL
Literal BOOLEAN_LITERAL
Literal CHAR_LITERAL
Literal STRING_LITERAL
Literal NULL_LITERAL
